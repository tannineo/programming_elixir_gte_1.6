# ch08 - Maps, Keyword Lists, Sets, and Structs

- [ch08 - Maps, Keyword Lists, Sets, and Structs](#ch08---maps-keyword-lists-sets-and-structs)
  - [How to Choose Between Maps, Structs, and Keyword Lists](#how-to-choose-between-maps-structs-and-keyword-lists)
  - [Keyword Lists](#keyword-lists)
  - [Maps](#maps)
  - [Pattern Matching and Updating Maps](#pattern-matching-and-updating-maps)
    - [Pattern Matching Can’t Bind Keys](#pattern-matching-cant-bind-keys)
    - [Pattern Matching Can Match Variable Keys](#pattern-matching-can-match-variable-keys)
  - [Updating a Map](#updating-a-map)
  - [Structs](#structs)
  - [Nested Dictionary Structures](#nested-dictionary-structures)
    - [Nested Accessors and Nonstructs](#nested-accessors-and-nonstructs)
    - [Dynamic (Runtime) Nested Accessors](#dynamic-runtime-nested-accessors)
    - [The `Access` Module](#the-access-module)
  - [Sets](#sets)
  - [With Great Power Comes Great Temptation](#with-great-power-comes-great-temptation)

## How to Choose Between Maps, Structs, and Keyword Lists

Decision goes when asking youself the questions:

- Do I want to pattern-match against the contents (for example, matching a dictionary that has a key of :name somewhere in it)?
  - If so, use a map.
- Will I want more than one entry with the same key?
  - If so, you’ll have to use the Keyword module.
- Do I need to guarantee the elements are ordered?
  - If so, again, use the Keyword module.
- Do I have a fixed set of fields (that is, is the structure of the data always the same)?
  - If so, use a struct.
- Otherwise, if you’ve reached this point,
  - Use a map.

## Keyword Lists

Use `Keyword` module to access keyword lists.

```elixir
defmodule Canvas do
  @defaults [ fg: "black", bg: "white", font: "Merriweather" ]
  def draw_text(text, options \\ []) do
    options = Keyword.merge(@defaults, options)
    IO.puts "Drawing text #{inspect(text)}"
    IO.puts "Foreground:  #{options[:fg]}"
    IO.puts "Background:  #{Keyword.get(options, :bg)}"
    IO.puts "Font:        #{Keyword.get(options, :font)}"
    IO.puts "Pattern:     #{Keyword.get(options, :pattern, "solid")}"
    IO.puts "Style:       #{inspect Keyword.get_values(options, :style)}"
  end
end

Canvas.draw_text("hello", fg: "red", style: "italic", style: "bold")

#=> Drawing text "hello"
#   Foreground:  red
#   Background:  white
#   Font:        Merriweather
#   Pattern:     solid
#   Style:       ["italic", "bold"]
```

## Maps

```elixir
map = %{ name: "Dave", likes: "Programming", where: "Dallas" }
#=> %{likes: "Programming", name: "Dave", where: "Dallas"}

Map.keys map
#=> [:likes, :name, :where]

Map.values map
#=> ["Programming", "Dave", "Dallas"]

map[:name]
#=> "Dave"

map.name
#=> "Dave"

map1 = Map.drop map, [:where, :likes]
#=> %{name: "Dave"}

map2 = Map.put map, :also_likes, "Ruby"
#=> %{also_likes: "Ruby", likes: "Programming", name: "Dave", where: "Dallas"}

Map.keys map2
#=> [:also_likes, :likes, :name, :where]

Map.has_key? map1, :where
#=> false

{ value, updated_map } = Map.pop map2, :also_likes
#=> {"Ruby", %{likes: "Programming", name: "Dave", where: "Dallas"}}

Map.equal? map, updated_map
#=> true
```

## Pattern Matching and Updating Maps

```elixir
person = %{ name: "Dave", height: 1.88 }

# Is there an entry with the key :name?
%{ name: a_name } = person
a_name
#=> "Dave"

# Are there entries for the keys :name and :height?
%{ name: _, height: _ } = person
#=> %{height: 1.88, name: "Dave"}
%{ name: _, weight: _ } = person
# ** (MatchError) no match of right hand side value: %{height: 1.88, name: "Dave"}

# Does the entry with key :name have the value "Dave"?
%{ name: "Dave" } = person
#=> %{height: 1.88, name: "Dave"}
```

Querying a list of maps.

```elixir
people = [
  %{ name: "Grumpy", height: 1.24 },
  %{ name: "Dave",   height: 1.88 },
  %{ name: "Dopey",  height: 1.32 }, # <= tailing comma is fine
]

IO.inspect(for person = %{ height: height } <- people, height > 1.5, do: person)
#=> [%{height: 1.88, name: "Dave"}]

defmodule HotelRoom do
  def book(%{name: name, height: height}) when height > 1.9 do
    IO.puts "Need extra-long bed for #{name}" end
  def book(%{name: name, height: height}) when height < 1.3 do
    IO.puts "Need low shower controls for #{name}" end
  def book(person) do
    IO.puts "Need regular bed for #{person.name}"
  end
end

people |> Enum.each(&HotelRoom.book/1)

#=> Need low shower controls for Grumpy
#   Need regular bed for Dave
#   Need regular bed for Dopey
```

### Pattern Matching Can’t Bind Keys

```text
iex(1)> %{ item => :ok } = %{ 1 => :ok, 2 => :error }
** (CompileError) iex:4: cannot use variable item as map key inside a pattern. Map keys in patterns can only be literals (such as atoms, strings, tuples, and the like) or an existing variable matched with the pin operator (such as ^some_var)
    (stdlib 3.13) lists.erl:1263: :lists.foldl/3
```

### Pattern Matching Can Match Variable Keys

```elixir
data = %{ name: "Dave", state: "TX", likes: "Elixir" }

for key <- [ :name, :likes ] do
  %{ ^key => value } = data
  value
end
#=> ["Dave", "Elixir"]
```

## Updating a Map

`new_map = %{ old_map | key => value, ... }`

```elixir
m = %{ a: 1, b: 2, c: 3 }

m1 = %{ m | b: "two", c: "three" }
#=> %{a: 1, b: "two", c: "three"}

m2 = %{ m1 | a: "one" }
#=> %{a: "one", b: "two", c: "three"}
```

## Structs

A struct is just a module that wraps a limited form of map.

- the keys must be atoms
- these 'maps' don’t have `Dict` capabilities.

```elixir
defmodule Subscriber do
  defstruct name: "", paid: false, over_18: true
end

s1 = %Subscriber{}
#=> %Subscriber{name: "", over_18: true, paid: false}

s2 = %Subscriber{ name: "Dave" }
#=> %Subscriber{name: "Dave", over_18: true, paid: false}

s3 = %Subscriber{ name: "Mary", paid: true }
#=> %Subscriber{name: "Mary", over_18: true, paid: true}

s3.name
#=> "Mary"

%Subscriber{name: a_name} = s3
#=> %Subscriber{name: "Mary", over_18: true, paid: true}

a_name
#=> "Mary"
```

Adding struct-specific behaviors:

```elixir
defmodule Attendee do
  defstruct name: "", paid: false, over_18: true

  def may_attend_after_party(attendee = %Attendee{}) do
    attendee.paid && attendee.over_18
  end

  def print_vip_badge(%Attendee{name: name}) when name != "" do
    IO.puts "Very cheap badge for #{name}"
  end

  def print_vip_badge(%Attendee{}) do
    raise "missing name for badge"
  end
end

a1 = %Attendee{name: "Dave", over_18: true}
#=> %Attendee{name: "Dave", over_18: true, paid: false}

Attendee.may_attend_after_party(a1)
#=> false

a2 = %Attendee{a1 | paid: true}
#=> %Attendee{name: "Dave", over_18: true, paid: true}

Attendee.may_attend_after_party(a2)
#=> true

Attendee.print_vip_badge(a2)
#=> Very cheap badge for Dave
#=> :ok

a3 = %Attendee{}
#=> %Attendee{name: "", over_18: true, paid: false}

Attendee.print_vip_badge(a3)
#=> ** (RuntimeError) missing name for badge ...
```

> Structs also play a large role when implementing polymorphism...

## Nested Dictionary Structures

```elixir
defmodule Customer do
  defstruct name: "", company: ""
end

defmodule BugReport do
  defstruct owner: %Customer{}, details: "", severity: 1
end

report = %BugReport{owner: %Customer{name: "Dave", company: "Pragmatic"}, details: "broken"}
#=> %BugReport{details: "broken", severity: 1, owner: %Customer{company: "Pragmatic", name: "Dave"}}

# We are trying to update the company name of the Customer
# Instead of using the long long pattern matching below:
report = %BugReport{ report | owner: %Customer{ report.owner | company: "PragProg" }}
#=> %BugReport{details: "broken", owner: %Customer{company: "PragProg", name: "Dave"}, severity: 1}

# We use built-in function to update that
report = put_in(report.owner.company, "PragProg")
#=> %BugReport{details: "broken", owner: %Customer{company: "PragProg", name: "Dave"}, severity: 1}

# Or apply a function to the value in a struct
report = update_in(report.owner.name, &("Mr. " <> &1))
#=> %BugReport{details: "broken", owner: %Customer{company: "PragProg", name: "Mr. Dave"}, severity: 1}
```

### Nested Accessors and Nonstructs

```elixir
report = %{ owner: %{ name: "Dave", company: "Pragmatic" }, severity: 1}
#=> %{owner: %{company: "Pragmatic", name: "Dave"}, severity: 1}

put_in(report[:owner][:company], "PragProg")
#=> %{owner: %{company: "PragProg", name: "Dave"}, severity: 1}

update_in(report[:owner][:name], &("Mr. " <> &1))
#=> %{owner: %{company: "Pragmatic", name: "Mr. Dave"}, severity: 1}
```

### Dynamic (Runtime) Nested Accessors

The nested accessors we’ve seen so far are **macros** — they operate at compile time. As a result, they have some limitations:

- The number of keys you pass a particular call is static.
- You can’t pass the set of keys as parameters between functions.

To overcome this, `get_in`, `put_in`, `update_in`, and `get_and_update_in` can all take a list of keys as a separate parameter. Adding this parameter changes them from macros to function calls, so they become dynamic.

|                   | Macro         | Function            |
| :---------------- | :------------ | :------------------ |
| get_in            | no            | (dict, keys)        |
| put_in            | (path, value) | (dict, keys, value) |
| update_in         | (path, fn)    | (dict, keys, fn)    |
| get_and_update_in | (path, fn)    | (dict, keys, fn)    |

```elixir
nested = %{
  buttercup: %{
    actor: %{
      first: "Robin",
      last: "Wright"
    },
    role: "princess"
  },
  westly: %{
    actor: %{
      first: "Cary",
      last: "Elwes"    # typo!
    },
    role: "farm boy"
  }
}

IO.inspect get_in(nested, [:buttercup])
#=> %{actor: %{first: "Robin", last: "Wright"}, role: "princess"}

IO.inspect get_in(nested, [:buttercup, :actor])
#=> %{first: "Robin", last: "Wright"}

IO.inspect get_in(nested, [:buttercup, :actor, :first])
#=> "Robin"

IO.inspect put_in(nested, [:westley, :actor, :last], "Elwes")
#=> %{buttercup: %{actor: %{first: "Robin", last: "Wright"}, role: "princess"},
#       westley: %{actor: %{first: "Cary", last: "Elwes"}, role: "farm boy"}}
```

### The `Access` Module

The `Access` module provides some predefined functions to use as parameters to `get` and `get_and_update_in` while traversing list of structs.

```elixir
cast = [
  %{
    character: "Buttercup",
    actor: %{
      first: "Robin",
      last:  "Wright"
    },
    role: "princess"
  },
  %{
    character: "Westley",
    actor: %{
      first: "Cary",
      last: "Elwes"
    },
    role: "farm boy"
  }
]

IO.inspect get_in(cast, [Access.all(), :character])
#=> ["Buttercup", "Westley"]

IO.inspect get_in(cast, [Access.at(1), :role])
#=> "farm boy"

IO.inspect get_and_update_in(cast, [Access.all(), :actor, :last],
                             fn (val) -> {val, String.upcase(val)} end)
#=> {["Wright", "Elwes"],
#    [%{actor: %{first: "Robin", last: "WRIGHT"}, character: "Buttercup",
#       role: "princess"},
#     %{actor: %{first: "Cary", last: "ELWES"}, character: "Westley",
#       role: "farm boy"}]}
```

The `elem` function works on tuples:

```elixir
cast = [
  %{
    character: "Buttercup",
    actor: {"Robin", "Wright"},
    role: "princess"
  },
  %{
    character: "Westley",
    actor: {"Carey", "Elwes"},    # Cary or Carey??? I'm not a movie fan...
    role: "farm boy"
  }
]

IO.inspect get_in(cast, [Access.all(), :actor, Access.elem(1)])
#=> ["Wright", "Elwes"]

IO.inspect get_and_update_in(cast, [Access.all(), :actor, Access.elem(1)],
                             fn (val) -> {val, String.reverse(val)} end)
#=> {["Wright", "Elwes"],
#    [%{actor: {"Robin", "thgirW"}, character: "Buttercup", role: "princess"},
#     %{actor: {"Carey", "sewlE"}, character: "Westley", role: "farm boy"}]}
```

`Access.pop` lets you remove the entry with a given key from a map or keyword list.

```elixir
Access.pop(%{name: "Elixir", creator: "Valim"}, :name)
#=> {"Elixir", %{creator: "Valim"}}

Access.pop([name: "Elixir", creator: "Valim"], :name)
#=> {"Elixir", [creator: "Valim"]}

Access.pop(%{name: "Elixir", creator: "Valim"}, :year)
#=> {nil, %{creator: "Valim", name: "Elixir"}}
```

## Sets

Set features are implemented by using `MapSet` module.

```elixir
set1 = 1..5 |> Enum.into(MapSet.new)
#=> #MapSet<[1, 2, 3, 4, 5]>

set2 = 3..8 |> Enum.into(MapSet.new)
#=> #MapSet<[3, 4, 5, 6, 7, 8]>

MapSet.member? set1, 3
#=> true

MapSet.union set1, set2
#=> #MapSet<[1, 2, 3, 4, 5, 6, 7, 8]>

MapSet.difference set1, set2
#=> #MapSet<[1, 2]>

MapSet.difference set2, set1
#=> #MapSet<[6, 7, 8]>

MapSet.intersection set2, set1
#=> #MapSet<[3, 4, 5]>
```

## With Great Power Comes Great Temptation

> "Hey, this is a bit like a class definition." And you’d be right.
> You can write something akin to object-oriented code using structs (or maps) and modules.
> This is a bad idea — not because objects are intrinsically bad, but because you’ll be mixing paradigms and diluting the benefits a functional approach gives you.

Stay pure and try to seperate functions and the data they work on.
