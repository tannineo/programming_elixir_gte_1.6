# ch05 - Anonymous Functions

- [ch05 - Anonymous Functions](#ch05---anonymous-functions)
  - [Functions and Pattern Matching](#functions-and-pattern-matching)
  - [One Function, Multiple Bodies](#one-function-multiple-bodies)
  - [Functions Can Return Functions](#functions-can-return-functions)
  - [Passing Functions as Arguments](#passing-functions-as-arguments)
  - [Pinned Values and Function Parameters](#pinned-values-and-function-parameters)
  - [The `&` Notation](#the--notation)

```elixir
sum = fn (a, b) -> a + b end
# or without () when declaring
sum = fn a, b -> a + b end
result = sum.(1, 2)

# or without args
great = fn -> IO.puts "Hello" end
great.()
```

## Functions and Pattern Matching

Pattern matching happens in the args of the function.

[Exercise: Functions-1](../code/ch05/functions_1.exs)

Create and run the functions that do the following:

```elixir
list_concat.([:a, :b], [:c, :d]) #=> [:a, :b, :c, :d]
sum.(1, 2, 3) #=> 6
pair_tuple_to_list.( { 1234, 5678 } ) #=> [ 1234, 5678 ]
```

## One Function, Multiple Bodies

Use pattern matching to write multiple bodies for the function.

```elixir
handle_open = fn
  {:ok, file} -> "Read data: #{IO.read(file, :line)}"
  {_, error} -> "Error: #{:file.format_error(error)}"
end
```

Where `:file` refers to the Erlang file module and `File` is the built-in Elixir module.

`#{...}` is Elixir’s string interpolation.

```text
iex(2)> handle_open.(File.open("README.md"))
"Read data: # programming_elixir_gte_1.6\n"

iex(3)> handle_open.(File.open("README.m")) # no such file
"Error: no such file or directory"
```

[Exercise: Functions-2](../code/ch05/functions_2.exs)
[Exercise: Functions-3](../code/ch05/functions_3.exs)

## Functions Can Return Functions

Closure.

```elixir
greeter = fn name ->
  fn ->
    "Hello #{name}"
  end
end

greeter.("Cary").() #=> Hello Cary
```

[Exercise: Functions-4](../code/ch05/functions_4.exs)

## Passing Functions as Arguments

The classic `map` function:

```text
iex> list = [1, 3, 5, 7, 9]
[1, 3, 5, 7, 9]

iex> Enum.map list, fn elem -> elem * 2 end
[2, 6, 10, 14, 18]
```

## Pinned Values and Function Parameters

In the example below we pinned the value `name` in argument pattern matching.

```elixir
defmodule Greeter do
  def for(name, greeting) do
    fn
      (^name) -> "#{greeting} #{name}"
      (_) -> "I don't know you"
    end
  end
end

mr_valim = Greeter.for("José", "Oi!")

IO.puts mr_valim.("José") # => Oi! José
IO.puts mr_valim.("Dave") # => I don't know you
```

## The `&` Notation

```elixir
add_one = &(&1 + 1) # same as add_one = fn (n) -> n + 1 end

```

Elixir can optimize away the anonymous function, replacing it with a direct reference to the function, in the example below is `&IO.puts/1` and `&Float.round/2`.

If the order is changed like `&(Float.round(&2, &1))`, there won't be this optimization.

```text
iex(2)> speak = &(IO.puts(&1))
&IO.puts/1

iex(3)> speak.("Hi")
Hi
:ok

iex(4)> rnd = &(Float.round(&1, &2))
&Float.round/2

iex(5)> rnd = &(Float.round(&2, &1))
#Function<43.97283095/2 in :erl_eval.expr/5>
```

Tuples and lists can be turned into functions using `&`:

```text
iex(5)> fun1 = &{&1, &2}
#Function<43.97283095/2 in :erl_eval.expr/5>

iex(6)> fun1.(1, 2)
{1, 2}

iex(7)> fun2 = &[&2, &1]
#Function<43.97283095/2 in :erl_eval.expr/5>

iex(8)> fun2.(3, 4)
[4, 3]
```

The `&` capture operator works with string (and string-like) literals:

```text
iex(9)> lily_n =  &"Lily and #{&1}"
#Function<44.97283095/1 in :erl_eval.expr/5>

iex(10)> lily_n.("Tiger")
"Lily and Tiger"
```

With regex:

```text
iex(1)> match_end = &~r/.*#{&1}$/
#Function<44.97283095/1 in :erl_eval.expr/5>
iex(2)> "cat" =~ match_end.("t")
true
iex(3)> "cat" =~ match_end.("c")
false
```

For `&` the capture operator, give it the name and arity (number of parameters) of an existing function, and it will return an anonymous function that calls it. (like `&IO.puts/1`)

```text
iex(4)> l = &length/1
&:erlang.length/1

iex(5)> l.([1,3,5,7])
4

iex(6)> len = &Enum.count/1
&Enum.count/1

iex(7)> len.([1,3,5,7])
4

iex(8)> m = &Kernel.min/2
&:erlang.min/2

iex(9)> m.(99,233)
99
```

[Exercise: Functions-5](../code/ch05/functions_5.exs)
